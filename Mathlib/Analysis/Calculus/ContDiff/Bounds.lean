/-
Copyright (c) 2019 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel, Floris van Doorn
-/
import Mathlib.Analysis.Calculus.ContDiff.Basic
import Mathlib.Data.Finset.Sym
import Mathlib.Data.Nat.Choose.Cast
import Mathlib.Data.Nat.Choose.Multinomial

#align_import analysis.calculus.cont_diff from "leanprover-community/mathlib"@"3bce8d800a6f2b8f63fe1e588fd76a9ff4adcebe"

/-!
# Bounds on higher derivatives

-/


noncomputable section

open scoped Classical BigOperators NNReal Nat

universe u uD uE uF uG

open Set Fin Filter Function

variable {ùïú : Type*} [NontriviallyNormedField ùïú] {D : Type uD} [NormedAddCommGroup D]
  [NormedSpace ùïú D] {E : Type uE} [NormedAddCommGroup E] [NormedSpace ùïú E] {F : Type uF}
  [NormedAddCommGroup F] [NormedSpace ùïú F] {G : Type uG} [NormedAddCommGroup G] [NormedSpace ùïú G]
  {s s‚ÇÅ t u : Set E}

/-!## Quantitative bounds -/

/-- Bounding the norm of the iterated derivative of `B (f x) (g x)` within a set in terms of the
iterated derivatives of `f` and `g` when `B` is bilinear. This lemma is an auxiliary version
assuming all spaces live in the same universe, to enable an induction. Use instead
`ContinuousLinearMap.norm_iteratedFDerivWithin_le_of_bilinear` that removes this assumption. -/
theorem ContinuousLinearMap.norm_iteratedFDerivWithin_le_of_bilinear_aux {Du Eu Fu Gu : Type u}
    [NormedAddCommGroup Du] [NormedSpace ùïú Du] [NormedAddCommGroup Eu] [NormedSpace ùïú Eu]
    [NormedAddCommGroup Fu] [NormedSpace ùïú Fu] [NormedAddCommGroup Gu] [NormedSpace ùïú Gu]
    (B : Eu ‚ÜíL[ùïú] Fu ‚ÜíL[ùïú] Gu) {f : Du ‚Üí Eu} {g : Du ‚Üí Fu} {n : ‚Ñï} {s : Set Du} {x : Du}
    (hf : ContDiffOn ùïú n f s) (hg : ContDiffOn ùïú n g s) (hs : UniqueDiffOn ùïú s) (hx : x ‚àà s) :
    ‚ÄñiteratedFDerivWithin ùïú n (fun y => B (f y) (g y)) s x‚Äñ ‚â§
      ‚ÄñB‚Äñ * ‚àë i in Finset.range (n + 1), (n.choose i : ‚Ñù) * ‚ÄñiteratedFDerivWithin ùïú i f s x‚Äñ *
        ‚ÄñiteratedFDerivWithin ùïú (n - i) g s x‚Äñ := by
  /- We argue by induction on `n`. The bound is trivial for `n = 0`. For `n + 1`, we write
    the `(n+1)`-th derivative as the `n`-th derivative of the derivative `B f g' + B f' g`,
    and apply the inductive assumption to each of those two terms. For this induction to make sense,
    the spaces of linear maps that appear in the induction should be in the same universe as the
    original spaces, which explains why we assume in the lemma that all spaces live in the same
    universe. -/
  induction' n with n IH generalizing Eu Fu Gu
  ¬∑ simp only [Nat.zero_eq, norm_iteratedFDerivWithin_zero, zero_add, Finset.range_one,
      Finset.sum_singleton, Nat.choose_self, Nat.cast_one, one_mul, Nat.sub_zero, ‚Üê mul_assoc]
    apply B.le_op_norm‚ÇÇ
  ¬∑ have In : (n : ‚Ñï‚àû) + 1 ‚â§ n.succ := by simp only [Nat.cast_succ, le_refl]
    -- Porting note: the next line is a hack allowing Lean to find the operator norm instance.
    let norm := @ContinuousLinearMap.hasOpNorm _ _ Eu ((Du ‚ÜíL[ùïú] Fu) ‚ÜíL[ùïú] Du ‚ÜíL[ùïú] Gu) _ _ _ _ _ _
      (RingHom.id ùïú)
    have I1 :
        ‚ÄñiteratedFDerivWithin ùïú n (fun y : Du => B.precompR Du (f y) (fderivWithin ùïú g s y)) s x‚Äñ ‚â§
          ‚ÄñB‚Äñ * ‚àë i : ‚Ñï in Finset.range (n + 1), n.choose i * ‚ÄñiteratedFDerivWithin ùïú i f s x‚Äñ *
            ‚ÄñiteratedFDerivWithin ùïú (n + 1 - i) g s x‚Äñ := by
      calc
        ‚ÄñiteratedFDerivWithin ùïú n (fun y : Du => B.precompR Du (f y) (fderivWithin ùïú g s y)) s x‚Äñ ‚â§
            ‚ÄñB.precompR Du‚Äñ * ‚àë i : ‚Ñï in Finset.range (n + 1),
              n.choose i * ‚ÄñiteratedFDerivWithin ùïú i f s x‚Äñ *
                ‚ÄñiteratedFDerivWithin ùïú (n - i) (fderivWithin ùïú g s) s x‚Äñ :=
          IH _ (hf.of_le (Nat.cast_le.2 (Nat.le_succ n))) (hg.fderivWithin hs In)
        _ ‚â§ ‚ÄñB‚Äñ * ‚àë i : ‚Ñï in Finset.range (n + 1), n.choose i * ‚ÄñiteratedFDerivWithin ùïú i f s x‚Äñ *
              ‚ÄñiteratedFDerivWithin ùïú (n - i) (fderivWithin ùïú g s) s x‚Äñ :=
          (mul_le_mul_of_nonneg_right (B.norm_precompR_le Du)
            (Finset.sum_nonneg' fun i => by positivity))
        _ = _ := by
          congr 1
          apply Finset.sum_congr rfl fun i hi => ?_
          rw [Nat.succ_sub (Nat.lt_succ_iff.1 (Finset.mem_range.1 hi)),
            ‚Üê norm_iteratedFDerivWithin_fderivWithin hs hx]
    -- Porting note: the next line is a hack allowing Lean to find the operator norm instance.
    let norm := @ContinuousLinearMap.hasOpNorm _ _ (Du ‚ÜíL[ùïú] Eu) (Fu ‚ÜíL[ùïú] Du ‚ÜíL[ùïú] Gu) _ _ _ _ _ _
      (RingHom.id ùïú)
    have I2 :
        ‚ÄñiteratedFDerivWithin ùïú n (fun y : Du => B.precompL Du (fderivWithin ùïú f s y) (g y)) s x‚Äñ ‚â§
        ‚ÄñB‚Äñ * ‚àë i : ‚Ñï in Finset.range (n + 1), n.choose i * ‚ÄñiteratedFDerivWithin ùïú (i + 1) f s x‚Äñ *
          ‚ÄñiteratedFDerivWithin ùïú (n - i) g s x‚Äñ :=
      calc
        ‚ÄñiteratedFDerivWithin ùïú n (fun y : Du => B.precompL Du (fderivWithin ùïú f s y) (g y)) s x‚Äñ ‚â§
            ‚ÄñB.precompL Du‚Äñ * ‚àë i : ‚Ñï in Finset.range (n + 1),
              n.choose i * ‚ÄñiteratedFDerivWithin ùïú i (fderivWithin ùïú f s) s x‚Äñ *
                ‚ÄñiteratedFDerivWithin ùïú (n - i) g s x‚Äñ :=
          IH _ (hf.fderivWithin hs In) (hg.of_le (Nat.cast_le.2 (Nat.le_succ n)))
        _ ‚â§ ‚ÄñB‚Äñ * ‚àë i : ‚Ñï in Finset.range (n + 1),
            n.choose i * ‚ÄñiteratedFDerivWithin ùïú i (fderivWithin ùïú f s) s x‚Äñ *
              ‚ÄñiteratedFDerivWithin ùïú (n - i) g s x‚Äñ :=
          (mul_le_mul_of_nonneg_right (B.norm_precompL_le Du)
            (Finset.sum_nonneg' fun i => by positivity))
        _ = _ := by
          congr 1
          apply Finset.sum_congr rfl fun i _ => ?_
          rw [‚Üê norm_iteratedFDerivWithin_fderivWithin hs hx]
    have J : iteratedFDerivWithin ùïú n
        (fun y : Du => fderivWithin ùïú (fun y : Du => B (f y) (g y)) s y) s x =
          iteratedFDerivWithin ùïú n (fun y => B.precompR Du (f y)
            (fderivWithin ùïú g s y) + B.precompL Du (fderivWithin ùïú f s y) (g y)) s x := by
      apply iteratedFDerivWithin_congr (fun y hy => ?_) hx
      have L : (1 : ‚Ñï‚àû) ‚â§ n.succ := by
        simpa only [ENat.coe_one, Nat.one_le_cast] using Nat.succ_pos n
      exact B.fderivWithin_of_bilinear (hf.differentiableOn L y hy) (hg.differentiableOn L y hy)
        (hs y hy)
    rw [‚Üê norm_iteratedFDerivWithin_fderivWithin hs hx, J]
    have A : ContDiffOn ùïú n (fun y => B.precompR Du (f y) (fderivWithin ùïú g s y)) s :=
      (B.precompR Du).isBoundedBilinearMap.contDiff.comp_contDiff_on‚ÇÇ
        (hf.of_le (Nat.cast_le.2 (Nat.le_succ n))) (hg.fderivWithin hs In)
    have A' : ContDiffOn ùïú n (fun y => B.precompL Du (fderivWithin ùïú f s y) (g y)) s :=
      (B.precompL Du).isBoundedBilinearMap.contDiff.comp_contDiff_on‚ÇÇ (hf.fderivWithin hs In)
        (hg.of_le (Nat.cast_le.2 (Nat.le_succ n)))
    rw [iteratedFDerivWithin_add_apply' A A' hs hx]
    apply (norm_add_le _ _).trans ((add_le_add I1 I2).trans (le_of_eq ?_))
    simp_rw [‚Üê mul_add, mul_assoc]
    congr 1
    exact (Finset.sum_choose_succ_mul
      (fun i j => ‚ÄñiteratedFDerivWithin ùïú i f s x‚Äñ * ‚ÄñiteratedFDerivWithin ùïú j g s x‚Äñ) n).symm
#align continuous_linear_map.norm_iterated_fderiv_within_le_of_bilinear_aux ContinuousLinearMap.norm_iteratedFDerivWithin_le_of_bilinear_aux

set_option maxHeartbeats 700000 in -- 3.5√ó the default limit
/-- Bounding the norm of the iterated derivative of `B (f x) (g x)` within a set in terms of the
iterated derivatives of `f` and `g` when `B` is bilinear:
`‚ÄñD^n (x ‚Ü¶ B (f x) (g x))‚Äñ ‚â§ ‚ÄñB‚Äñ ‚àë_{k ‚â§ n} n.choose k ‚ÄñD^k f‚Äñ ‚ÄñD^{n-k} g‚Äñ` -/
theorem ContinuousLinearMap.norm_iteratedFDerivWithin_le_of_bilinear (B : E ‚ÜíL[ùïú] F ‚ÜíL[ùïú] G)
    {f : D ‚Üí E} {g : D ‚Üí F} {N : ‚Ñï‚àû} {s : Set D} {x : D} (hf : ContDiffOn ùïú N f s)
    (hg : ContDiffOn ùïú N g s) (hs : UniqueDiffOn ùïú s) (hx : x ‚àà s) {n : ‚Ñï} (hn : (n : ‚Ñï‚àû) ‚â§ N) :
    ‚ÄñiteratedFDerivWithin ùïú n (fun y => B (f y) (g y)) s x‚Äñ ‚â§
      ‚ÄñB‚Äñ * ‚àë i in Finset.range (n + 1), (n.choose i : ‚Ñù) * ‚ÄñiteratedFDerivWithin ùïú i f s x‚Äñ *
        ‚ÄñiteratedFDerivWithin ùïú (n - i) g s x‚Äñ := by
  /- We reduce the bound to the case where all spaces live in the same universe (in which we
    already have proved the result), by using linear isometries between the spaces and their `ULift`
    to a common universe. These linear isometries preserve the norm of the iterated derivative. -/
  let Du : Type max uD uE uF uG := ULift.{max uE uF uG, uD} D
  let Eu : Type max uD uE uF uG := ULift.{max uD uF uG, uE} E
  let Fu : Type max uD uE uF uG := ULift.{max uD uE uG, uF} F
  let Gu : Type max uD uE uF uG := ULift.{max uD uE uF, uG} G
  have isoD : Du ‚âÉ‚Çó·µ¢[ùïú] D := LinearIsometryEquiv.ulift ùïú D
  have isoE : Eu ‚âÉ‚Çó·µ¢[ùïú] E := LinearIsometryEquiv.ulift ùïú E
  have isoF : Fu ‚âÉ‚Çó·µ¢[ùïú] F := LinearIsometryEquiv.ulift ùïú F
  have isoG : Gu ‚âÉ‚Çó·µ¢[ùïú] G := LinearIsometryEquiv.ulift ùïú G
  -- lift `f` and `g` to versions `fu` and `gu` on the lifted spaces.
  set fu : Du ‚Üí Eu := isoE.symm ‚àò f ‚àò isoD with hfu
  set gu : Du ‚Üí Fu := isoF.symm ‚àò g ‚àò isoD with hgu
  -- lift the bilinear map `B` to a bilinear map `Bu` on the lifted spaces.
  set Bu‚ÇÄ : Eu ‚ÜíL[ùïú] Fu ‚ÜíL[ùïú] G := ((B.comp (isoE : Eu ‚ÜíL[ùïú] E)).flip.comp (isoF : Fu ‚ÜíL[ùïú] F)).flip
    with hBu‚ÇÄ
  let Bu : Eu ‚ÜíL[ùïú] Fu ‚ÜíL[ùïú] Gu;
  exact ContinuousLinearMap.compL ùïú Eu (Fu ‚ÜíL[ùïú] G) (Fu ‚ÜíL[ùïú] Gu)
    (ContinuousLinearMap.compL ùïú Fu G Gu (isoG.symm : G ‚ÜíL[ùïú] Gu)) Bu‚ÇÄ
  have hBu : Bu = ContinuousLinearMap.compL ùïú Eu (Fu ‚ÜíL[ùïú] G) (Fu ‚ÜíL[ùïú] Gu)
      (ContinuousLinearMap.compL ùïú Fu G Gu (isoG.symm : G ‚ÜíL[ùïú] Gu)) Bu‚ÇÄ := rfl
  have Bu_eq : (fun y => Bu (fu y) (gu y)) = isoG.symm ‚àò (fun y => B (f y) (g y)) ‚àò isoD := by
    ext1 y
    -- Porting note: the two blocks of `rw`s below were
    -- ```
    -- simp only [ContinuousLinearMap.compL_apply, Function.comp_apply,
    --   ContinuousLinearMap.coe_comp', LinearIsometryEquiv.coe_coe'',
    --   ContinuousLinearMap.flip_apply, LinearIsometryEquiv.apply_symm_apply]
    -- ```
    rw [hBu]
    iterate 2 rw [ContinuousLinearMap.compL_apply, ContinuousLinearMap.coe_comp',
      Function.comp_apply]
    rw [hBu‚ÇÄ]
    iterate 2 rw [ContinuousLinearMap.flip_apply, ContinuousLinearMap.coe_comp',
      Function.comp_apply]
    rw [hfu, Function.comp_apply, LinearIsometryEquiv.coe_coe'', LinearIsometryEquiv.coe_coe'',
      LinearIsometryEquiv.apply_symm_apply isoE, Function.comp_apply,
      hgu, LinearIsometryEquiv.coe_coe'', Function.comp_apply,
      LinearIsometryEquiv.apply_symm_apply isoF]
    simp only [Function.comp_apply]
  -- All norms are preserved by the lifting process.
  have Bu_le : ‚ÄñBu‚Äñ ‚â§ ‚ÄñB‚Äñ := by
    refine' ContinuousLinearMap.op_norm_le_bound _ (norm_nonneg _) fun y => _
    refine' ContinuousLinearMap.op_norm_le_bound _ (by positivity) fun x => _
    simp only [ContinuousLinearMap.compL_apply, ContinuousLinearMap.coe_comp',
      Function.comp_apply, LinearIsometryEquiv.coe_coe'', ContinuousLinearMap.flip_apply,
      LinearIsometryEquiv.norm_map]
    rw [ContinuousLinearMap.coe_comp', Function.comp_apply, ContinuousLinearMap.compL_apply,
      ContinuousLinearMap.coe_comp', Function.comp_apply]
    iterate 2 rw [ContinuousLinearMap.flip_apply, ContinuousLinearMap.coe_comp',
      Function.comp_apply]
    simp only [LinearIsometryEquiv.coe_coe'', LinearIsometryEquiv.norm_map]
    calc
      ‚ÄñB (isoE y) (isoF x)‚Äñ ‚â§ ‚ÄñB (isoE y)‚Äñ * ‚ÄñisoF x‚Äñ := ContinuousLinearMap.le_op_norm _ _
      _ ‚â§ ‚ÄñB‚Äñ * ‚ÄñisoE y‚Äñ * ‚ÄñisoF x‚Äñ := by gcongr; apply ContinuousLinearMap.le_op_norm
      _ = ‚ÄñB‚Äñ * ‚Äñy‚Äñ * ‚Äñx‚Äñ := by simp only [LinearIsometryEquiv.norm_map]
  let su := isoD ‚Åª¬π' s
  have hsu : UniqueDiffOn ùïú su := isoD.toContinuousLinearEquiv.uniqueDiffOn_preimage_iff.2 hs
  let xu := isoD.symm x
  have hxu : xu ‚àà su := by
    simpa only [Set.mem_preimage, LinearIsometryEquiv.apply_symm_apply] using hx
  have xu_x : isoD xu = x := by simp only [LinearIsometryEquiv.apply_symm_apply]
  have hfu : ContDiffOn ùïú n fu su :=
    isoE.symm.contDiff.comp_contDiffOn
      ((hf.of_le hn).comp_continuousLinearMap (isoD : Du ‚ÜíL[ùïú] D))
  have hgu : ContDiffOn ùïú n gu su :=
    isoF.symm.contDiff.comp_contDiffOn
      ((hg.of_le hn).comp_continuousLinearMap (isoD : Du ‚ÜíL[ùïú] D))
  have Nfu : ‚àÄ i, ‚ÄñiteratedFDerivWithin ùïú i fu su xu‚Äñ = ‚ÄñiteratedFDerivWithin ùïú i f s x‚Äñ := by
    intro i
    rw [LinearIsometryEquiv.norm_iteratedFDerivWithin_comp_left _ _ hsu hxu]
    rw [LinearIsometryEquiv.norm_iteratedFDerivWithin_comp_right _ _ hs, xu_x]
    rwa [‚Üê xu_x] at hx
  have Ngu : ‚àÄ i, ‚ÄñiteratedFDerivWithin ùïú i gu su xu‚Äñ = ‚ÄñiteratedFDerivWithin ùïú i g s x‚Äñ := by
    intro i
    rw [LinearIsometryEquiv.norm_iteratedFDerivWithin_comp_left _ _ hsu hxu]
    rw [LinearIsometryEquiv.norm_iteratedFDerivWithin_comp_right _ _ hs, xu_x]
    rwa [‚Üê xu_x] at hx
  have NBu :
    ‚ÄñiteratedFDerivWithin ùïú n (fun y => Bu (fu y) (gu y)) su xu‚Äñ =
      ‚ÄñiteratedFDerivWithin ùïú n (fun y => B (f y) (g y)) s x‚Äñ := by
    rw [Bu_eq]
    rw [LinearIsometryEquiv.norm_iteratedFDerivWithin_comp_left _ _ hsu hxu]
    rw [LinearIsometryEquiv.norm_iteratedFDerivWithin_comp_right _ _ hs, xu_x]
    rwa [‚Üê xu_x] at hx
  -- state the bound for the lifted objects, and deduce the original bound from it.
  have : ‚ÄñiteratedFDerivWithin ùïú n (fun y => Bu (fu y) (gu y)) su xu‚Äñ ‚â§
      ‚ÄñBu‚Äñ * ‚àë i in Finset.range (n + 1), (n.choose i : ‚Ñù) * ‚ÄñiteratedFDerivWithin ùïú i fu su xu‚Äñ *
        ‚ÄñiteratedFDerivWithin ùïú (n - i) gu su xu‚Äñ :=
    Bu.norm_iteratedFDerivWithin_le_of_bilinear_aux hfu hgu hsu hxu
  simp only [Nfu, Ngu, NBu] at this
  apply this.trans (mul_le_mul_of_nonneg_right Bu_le ?_)
  exact Finset.sum_nonneg' fun i => by positivity
#align continuous_linear_map.norm_iterated_fderiv_within_le_of_bilinear ContinuousLinearMap.norm_iteratedFDerivWithin_le_of_bilinear

/-- Bounding the norm of the iterated derivative of `B (f x) (g x)` in terms of the
iterated derivatives of `f` and `g` when `B` is bilinear:
`‚ÄñD^n (x ‚Ü¶ B (f x) (g x))‚Äñ ‚â§ ‚ÄñB‚Äñ ‚àë_{k ‚â§ n} n.choose k ‚ÄñD^k f‚Äñ ‚ÄñD^{n-k} g‚Äñ` -/
theorem ContinuousLinearMap.norm_iteratedFDeriv_le_of_bilinear (B : E ‚ÜíL[ùïú] F ‚ÜíL[ùïú] G) {f : D ‚Üí E}
    {g : D ‚Üí F} {N : ‚Ñï‚àû} (hf : ContDiff ùïú N f) (hg : ContDiff ùïú N g) (x : D) {n : ‚Ñï}
    (hn : (n : ‚Ñï‚àû) ‚â§ N) :
    ‚ÄñiteratedFDeriv ùïú n (fun y => B (f y) (g y)) x‚Äñ ‚â§ ‚ÄñB‚Äñ * ‚àë i in Finset.range (n + 1),
      (n.choose i : ‚Ñù) * ‚ÄñiteratedFDeriv ùïú i f x‚Äñ * ‚ÄñiteratedFDeriv ùïú (n - i) g x‚Äñ := by
  simp_rw [‚Üê iteratedFDerivWithin_univ]
  exact B.norm_iteratedFDerivWithin_le_of_bilinear hf.contDiffOn hg.contDiffOn uniqueDiffOn_univ
    (mem_univ x) hn
#align continuous_linear_map.norm_iterated_fderiv_le_of_bilinear ContinuousLinearMap.norm_iteratedFDeriv_le_of_bilinear

/-- Bounding the norm of the iterated derivative of `B (f x) (g x)` within a set in terms of the
iterated derivatives of `f` and `g` when `B` is bilinear of norm at most `1`:
`‚ÄñD^n (x ‚Ü¶ B (f x) (g x))‚Äñ ‚â§ ‚àë_{k ‚â§ n} n.choose k ‚ÄñD^k f‚Äñ ‚ÄñD^{n-k} g‚Äñ` -/
theorem ContinuousLinearMap.norm_iteratedFDerivWithin_le_of_bilinear_of_le_one
    (B : E ‚ÜíL[ùïú] F ‚ÜíL[ùïú] G) {f : D ‚Üí E} {g : D ‚Üí F} {N : ‚Ñï‚àû} {s : Set D} {x : D}
    (hf : ContDiffOn ùïú N f s) (hg : ContDiffOn ùïú N g s) (hs : UniqueDiffOn ùïú s) (hx : x ‚àà s) {n : ‚Ñï}
    (hn : (n : ‚Ñï‚àû) ‚â§ N) (hB : ‚ÄñB‚Äñ ‚â§ 1) : ‚ÄñiteratedFDerivWithin ùïú n (fun y => B (f y) (g y)) s x‚Äñ ‚â§
      ‚àë i in Finset.range (n + 1), (n.choose i : ‚Ñù) * ‚ÄñiteratedFDerivWithin ùïú i f s x‚Äñ *
        ‚ÄñiteratedFDerivWithin ùïú (n - i) g s x‚Äñ := by
  apply (B.norm_iteratedFDerivWithin_le_of_bilinear hf hg hs hx hn).trans
  apply mul_le_of_le_one_left (Finset.sum_nonneg' fun i => ?_) hB
  positivity
#align continuous_linear_map.norm_iterated_fderiv_within_le_of_bilinear_of_le_one ContinuousLinearMap.norm_iteratedFDerivWithin_le_of_bilinear_of_le_one

/-- Bounding the norm of the iterated derivative of `B (f x) (g x)` in terms of the
iterated derivatives of `f` and `g` when `B` is bilinear of norm at most `1`:
`‚ÄñD^n (x ‚Ü¶ B (f x) (g x))‚Äñ ‚â§ ‚àë_{k ‚â§ n} n.choose k ‚ÄñD^k f‚Äñ ‚ÄñD^{n-k} g‚Äñ` -/
theorem ContinuousLinearMap.norm_iteratedFDeriv_le_of_bilinear_of_le_one (B : E ‚ÜíL[ùïú] F ‚ÜíL[ùïú] G)
    {f : D ‚Üí E} {g : D ‚Üí F} {N : ‚Ñï‚àû} (hf : ContDiff ùïú N f) (hg : ContDiff ùïú N g) (x : D) {n : ‚Ñï}
    (hn : (n : ‚Ñï‚àû) ‚â§ N) (hB : ‚ÄñB‚Äñ ‚â§ 1) : ‚ÄñiteratedFDeriv ùïú n (fun y => B (f y) (g y)) x‚Äñ ‚â§
      ‚àë i in Finset.range (n + 1),
        (n.choose i : ‚Ñù) * ‚ÄñiteratedFDeriv ùïú i f x‚Äñ * ‚ÄñiteratedFDeriv ùïú (n - i) g x‚Äñ := by
  simp_rw [‚Üê iteratedFDerivWithin_univ]
  exact B.norm_iteratedFDerivWithin_le_of_bilinear_of_le_one hf.contDiffOn hg.contDiffOn
    uniqueDiffOn_univ (mem_univ x) hn hB
#align continuous_linear_map.norm_iterated_fderiv_le_of_bilinear_of_le_one ContinuousLinearMap.norm_iteratedFDeriv_le_of_bilinear_of_le_one

section

variable {ùïú' : Type*} [NormedField ùïú'] [NormedAlgebra ùïú ùïú'] [NormedSpace ùïú' F]
  [IsScalarTower ùïú ùïú' F]

theorem norm_iteratedFDerivWithin_smul_le {f : E ‚Üí ùïú'} {g : E ‚Üí F} {N : ‚Ñï‚àû}
    (hf : ContDiffOn ùïú N f s) (hg : ContDiffOn ùïú N g s) (hs : UniqueDiffOn ùïú s) {x : E} (hx : x ‚àà s)
    {n : ‚Ñï} (hn : (n : ‚Ñï‚àû) ‚â§ N) : ‚ÄñiteratedFDerivWithin ùïú n (fun y => f y ‚Ä¢ g y) s x‚Äñ ‚â§
      ‚àë i in Finset.range (n + 1), (n.choose i : ‚Ñù) * ‚ÄñiteratedFDerivWithin ùïú i f s x‚Äñ *
        ‚ÄñiteratedFDerivWithin ùïú (n - i) g s x‚Äñ :=
  (ContinuousLinearMap.lsmul ùïú ùïú' :
    ùïú' ‚ÜíL[ùïú] F ‚ÜíL[ùïú] F).norm_iteratedFDerivWithin_le_of_bilinear_of_le_one
      hf hg hs hx hn ContinuousLinearMap.op_norm_lsmul_le
#align norm_iterated_fderiv_within_smul_le norm_iteratedFDerivWithin_smul_le

theorem norm_iteratedFDeriv_smul_le {f : E ‚Üí ùïú'} {g : E ‚Üí F} {N : ‚Ñï‚àû} (hf : ContDiff ùïú N f)
    (hg : ContDiff ùïú N g) (x : E) {n : ‚Ñï} (hn : (n : ‚Ñï‚àû) ‚â§ N) :
    ‚ÄñiteratedFDeriv ùïú n (fun y => f y ‚Ä¢ g y) x‚Äñ ‚â§ ‚àë i in Finset.range (n + 1),
      (n.choose i : ‚Ñù) * ‚ÄñiteratedFDeriv ùïú i f x‚Äñ * ‚ÄñiteratedFDeriv ùïú (n - i) g x‚Äñ :=
  (ContinuousLinearMap.lsmul ùïú ùïú' : ùïú' ‚ÜíL[ùïú] F ‚ÜíL[ùïú] F).norm_iteratedFDeriv_le_of_bilinear_of_le_one
    hf hg x hn ContinuousLinearMap.op_norm_lsmul_le
#align norm_iterated_fderiv_smul_le norm_iteratedFDeriv_smul_le

end

section

variable {A : Type*} [NormedRing A] [NormedAlgebra ùïú A]
variable {A' : Type*} [NormedCommRing A'] [NormedAlgebra ùïú A']

theorem norm_iteratedFDerivWithin_mul_le {f : E ‚Üí A} {g : E ‚Üí A} {N : ‚Ñï‚àû} (hf : ContDiffOn ùïú N f s)
    (hg : ContDiffOn ùïú N g s) (hs : UniqueDiffOn ùïú s) {x : E} (hx : x ‚àà s) {n : ‚Ñï}
    (hn : (n : ‚Ñï‚àû) ‚â§ N) : ‚ÄñiteratedFDerivWithin ùïú n (fun y => f y * g y) s x‚Äñ ‚â§
      ‚àë i in Finset.range (n + 1), (n.choose i : ‚Ñù) * ‚ÄñiteratedFDerivWithin ùïú i f s x‚Äñ *
        ‚ÄñiteratedFDerivWithin ùïú (n - i) g s x‚Äñ :=
  (ContinuousLinearMap.mul ùïú A :
    A ‚ÜíL[ùïú] A ‚ÜíL[ùïú] A).norm_iteratedFDerivWithin_le_of_bilinear_of_le_one
      hf hg hs hx hn (ContinuousLinearMap.op_norm_mul_le _ _)
#align norm_iterated_fderiv_within_mul_le norm_iteratedFDerivWithin_mul_le

theorem norm_iteratedFDeriv_mul_le {f : E ‚Üí A} {g : E ‚Üí A} {N : ‚Ñï‚àû} (hf : ContDiff ùïú N f)
    (hg : ContDiff ùïú N g) (x : E) {n : ‚Ñï} (hn : (n : ‚Ñï‚àû) ‚â§ N) :
    ‚ÄñiteratedFDeriv ùïú n (fun y => f y * g y) x‚Äñ ‚â§ ‚àë i in Finset.range (n + 1),
      (n.choose i : ‚Ñù) * ‚ÄñiteratedFDeriv ùïú i f x‚Äñ * ‚ÄñiteratedFDeriv ùïú (n - i) g x‚Äñ := by
  simp_rw [‚Üê iteratedFDerivWithin_univ]
  exact norm_iteratedFDerivWithin_mul_le
    hf.contDiffOn hg.contDiffOn uniqueDiffOn_univ (mem_univ x) hn
#align norm_iterated_fderiv_mul_le norm_iteratedFDeriv_mul_le

-- TODO: Move.
@[simp]
theorem Sym.card_coe {Œ± : Type*} {n : ‚Ñï} (s : Sym Œ± n) : Multiset.card (s : Multiset Œ±) = n :=
  s.prop

theorem norm_iteratedFDeriv_prod_le [NormOneClass A'] {Œπ : Type*} [DecidableEq Œπ] {u : Finset Œπ}
    {f : Œπ ‚Üí E ‚Üí A'} {N : ‚Ñï‚àû} (hf : ‚àÄ i ‚àà u, ContDiff ùïú N (f i)) (x : E)
    {n : ‚Ñï} (hn : (n : ‚Ñï‚àû) ‚â§ N) :
    ‚ÄñiteratedFDeriv ùïú n (‚àè j in u, f j ¬∑) x‚Äñ ‚â§
      ‚àë p in u.sym n, (p : Multiset Œπ).multinomial *
        ‚àè j in u, ‚ÄñiteratedFDeriv ùïú ((p : Multiset Œπ).count j) (f j) x‚Äñ := by
  induction u using Finset.induction generalizing n with
  | empty =>
    cases n with
    | zero => simp [Sym.eq_nil_of_card_zero]
    | succ n => simp [iteratedFDeriv_succ_const]
  | @insert i u hi IH =>
    conv => lhs; simp only [Finset.prod_insert hi]
    simp only [Finset.mem_insert, forall_eq_or_imp] at hf
    specialize @IH hf.2
    refine le_trans
      (norm_iteratedFDeriv_mul_le hf.1 (contDiff_prod hf.2) _ hn) ?_
    rw [‚Üê Finset.sum_coe_sort (Finset.sym _ _)]
    rw [Finset.sum_equiv (Finset.symInsertEquiv hi) (t := Finset.univ)
      (g := fun (p : (k : Fin n.succ) √ó u.sym (n - k)) ‚Ü¶
        ((Finset.symInsertEquiv hi).symm p : Multiset Œπ).multinomial * ‚àè j in insert i u,
          ‚ÄñiteratedFDeriv ùïú (((Finset.symInsertEquiv hi).symm p : Multiset Œπ).count j) (f j) x‚Äñ)
      (by simp)
      (by
        simp only [Equiv.symm_apply_apply]
        intro p _
        -- TODO: Why doesn't `simp only` reach inside the product?
        -- have := Equiv.symm_apply_apply (Finset.symInsertEquiv hi) p
        -- simp only [Equiv.symm_apply_apply (Finset.symInsertEquiv hi) p]
        refine congrArg _ ?_
        refine congrArg _ ?_
        funext j
        rw [Equiv.symm_apply_apply])]
    rw [‚Üê Finset.univ_sigma_univ, Finset.sum_sigma, Finset.sum_range]
    refine Finset.sum_le_sum ?_
    simp only [Finset.mem_univ, forall_true_left]
    intro m
    simp only [Finset.symInsertEquiv_symm_apply_coe]
    specialize @IH (n - m) (le_trans (WithTop.coe_le_coe.mpr (n.sub_le m)) hn)
    refine le_trans (mul_le_mul_of_nonneg_left IH (by simp [mul_nonneg])) ?_
    rw [Finset.mul_sum, ‚Üê Finset.sum_coe_sort]
    refine Finset.sum_le_sum ?_
    simp only [Finset.mem_univ, forall_true_left, Subtype.forall]
    intro p hp
    refine le_of_eq ?_
    rw [Finset.prod_insert hi]
    have hip : i ‚àâ p := fun h ‚Ü¶ hi <| (Finset.mem_sym_iff.mp hp) i h
    have h_count : Multiset.count i (Sym.fill i m p : Multiset Œπ) = m := by
      simp only [Sym.coe_fill, Sym.coe_replicate]
      simp only [Multiset.count_add, Multiset.count_replicate_self]
      rw [Multiset.count_eq_zero_of_not_mem (by simpa [Sym.mem_coe] using hip)]
      simp
    have h_multinomial : (Sym.fill i m p : Multiset Œπ).multinomial =
        n.choose m * (p : Multiset Œπ).multinomial := by
      rw [Multiset.multinomial_filter_ne i]
      simp only [Sym.card_coe, h_count]
      simp only [Sym.coe_fill, Sym.coe_replicate, Multiset.filter_add]
      rw [Multiset.filter_eq_self.mpr]
      ¬∑ rw [Multiset.filter_eq_nil.mpr]
        ¬∑ simp
        ¬∑ exact fun j hj ‚Ü¶ Decidable.not_not.mpr (Multiset.eq_of_mem_replicate hj).symm
      ¬∑ exact fun j hj h ‚Ü¶ hip <| by simpa [h] using hj
    rw [h_count, h_multinomial, Nat.cast_mul]
    ring_nf  -- TODO: Was it just lucky that this worked?
    refine congrArg _ (Finset.prod_congr rfl ?_)
    intro j hj
    have hji : j ‚â† i := fun h ‚Ü¶ hi <| by simpa [h] using hj
    simp only [Sym.coe_fill]
    -- simp only [Multiset.count_add]  -- Example of simp not working.
    rw [Multiset.count_add]
    simp only [Sym.coe_replicate]
    rw [Multiset.count_eq_zero_of_not_mem (s := Multiset.replicate m i)]
    ¬∑ simp
    ¬∑ simp [Multiset.mem_replicate, hji]

end

/-- If the derivatives within a set of `g` at `f x` are bounded by `C`, and the `i`-th derivative
within a set of `f` at `x` is bounded by `D^i` for all `1 ‚â§ i ‚â§ n`, then the `n`-th derivative
of `g ‚àò f` is bounded by `n! * C * D^n`.
This lemma proves this estimate assuming additionally that two of the spaces live in the same
universe, to make an induction possible. Use instead `norm_iteratedFDerivWithin_comp_le` that
removes this assumption. -/
theorem norm_iteratedFDerivWithin_comp_le_aux {Fu Gu : Type u} [NormedAddCommGroup Fu]
    [NormedSpace ùïú Fu] [NormedAddCommGroup Gu] [NormedSpace ùïú Gu] {g : Fu ‚Üí Gu} {f : E ‚Üí Fu} {n : ‚Ñï}
    {s : Set E} {t : Set Fu} {x : E} (hg : ContDiffOn ùïú n g t) (hf : ContDiffOn ùïú n f s)
    (ht : UniqueDiffOn ùïú t) (hs : UniqueDiffOn ùïú s) (hst : MapsTo f s t) (hx : x ‚àà s) {C : ‚Ñù}
    {D : ‚Ñù} (hC : ‚àÄ i, i ‚â§ n ‚Üí ‚ÄñiteratedFDerivWithin ùïú i g t (f x)‚Äñ ‚â§ C)
    (hD : ‚àÄ i, 1 ‚â§ i ‚Üí i ‚â§ n ‚Üí ‚ÄñiteratedFDerivWithin ùïú i f s x‚Äñ ‚â§ D ^ i) :
    ‚ÄñiteratedFDerivWithin ùïú n (g ‚àò f) s x‚Äñ ‚â§ n ! * C * D ^ n := by
  /- We argue by induction on `n`, using that `D^(n+1) (g ‚àò f) = D^n (g ' ‚àò f ‚¨ù f')`. The successive
    derivatives of `g' ‚àò f` are controlled thanks to the inductive assumption, and those of `f'` are
    controlled by assumption.
    As composition of linear maps is a bilinear map, one may use
    `ContinuousLinearMap.norm_iteratedFDeriv_le_of_bilinear_of_le_one` to get from these a bound
    on `D^n (g ' ‚àò f ‚¨ù f')`. -/
  induction' n using Nat.case_strong_induction_on with n IH generalizing Gu
  ¬∑ simpa [norm_iteratedFDerivWithin_zero, Nat.factorial_zero, algebraMap.coe_one, one_mul,
      pow_zero, mul_one, comp_apply] using hC 0 le_rfl
  have M : (n : ‚Ñï‚àû) < n.succ := Nat.cast_lt.2 n.lt_succ_self
  have Cnonneg : 0 ‚â§ C := (norm_nonneg _).trans (hC 0 bot_le)
  have Dnonneg : 0 ‚â§ D := by
    have : 1 ‚â§ n + 1 := by simp only [le_add_iff_nonneg_left, zero_le']
    simpa only [pow_one] using (norm_nonneg _).trans (hD 1 le_rfl this)
  -- use the inductive assumption to bound the derivatives of `g' ‚àò f`.
  have I : ‚àÄ i ‚àà Finset.range (n + 1),
      ‚ÄñiteratedFDerivWithin ùïú i (fderivWithin ùïú g t ‚àò f) s x‚Äñ ‚â§ i ! * C * D ^ i
  ¬∑ intro i hi
    simp only [Finset.mem_range_succ_iff] at hi
    apply IH i hi
    ¬∑ apply hg.fderivWithin ht
      simp only [Nat.cast_succ]
      exact add_le_add_right (Nat.cast_le.2 hi) _
    ¬∑ apply hf.of_le (Nat.cast_le.2 (hi.trans n.le_succ))
    ¬∑ intro j hj
      have : ‚ÄñiteratedFDerivWithin ùïú j (fderivWithin ùïú g t) t (f x)‚Äñ =
          ‚ÄñiteratedFDerivWithin ùïú (j + 1) g t (f x)‚Äñ :=
        by rw [iteratedFDerivWithin_succ_eq_comp_right ht (hst hx), comp_apply,
          LinearIsometryEquiv.norm_map]
      rw [this]
      exact hC (j + 1) (add_le_add (hj.trans hi) le_rfl)
    ¬∑ intro j hj h'j
      exact hD j hj (h'j.trans (hi.trans n.le_succ))
  -- reformulate `hD` as a bound for the derivatives of `f'`.
  have J : ‚àÄ i, ‚ÄñiteratedFDerivWithin ùïú (n - i) (fderivWithin ùïú f s) s x‚Äñ ‚â§ D ^ (n - i + 1) := by
    intro i
    have : ‚ÄñiteratedFDerivWithin ùïú (n - i) (fderivWithin ùïú f s) s x‚Äñ =
        ‚ÄñiteratedFDerivWithin ùïú (n - i + 1) f s x‚Äñ
    ¬∑ rw [iteratedFDerivWithin_succ_eq_comp_right hs hx, comp_apply, LinearIsometryEquiv.norm_map]
    rw [this]
    apply hD
    ¬∑ simp only [le_add_iff_nonneg_left, zero_le']
    ¬∑ apply Nat.succ_le_succ tsub_le_self
  -- Now put these together: first, notice that we have to bound `D^n (g' ‚àò f ‚¨ù f')`.
  calc
    ‚ÄñiteratedFDerivWithin ùïú (n + 1) (g ‚àò f) s x‚Äñ =
        ‚ÄñiteratedFDerivWithin ùïú n (fun y : E => fderivWithin ùïú (g ‚àò f) s y) s x‚Äñ := by
      rw [iteratedFDerivWithin_succ_eq_comp_right hs hx, comp_apply,
        LinearIsometryEquiv.norm_map]
    _ = ‚ÄñiteratedFDerivWithin ùïú n (fun y : E => ContinuousLinearMap.compL ùïú E Fu Gu
        (fderivWithin ùïú g t (f y)) (fderivWithin ùïú f s y)) s x‚Äñ := by
      have L : (1 : ‚Ñï‚àû) ‚â§ n.succ := by simpa only [ENat.coe_one, Nat.one_le_cast] using n.succ_pos
      congr 1
      refine' iteratedFDerivWithin_congr (fun y hy => _) hx _
      apply fderivWithin.comp _ _ _ hst (hs y hy)
      ¬∑ exact hg.differentiableOn L _ (hst hy)
      ¬∑ exact hf.differentiableOn L _ hy
    -- bound it using the fact that the composition of linear maps is a bilinear operation,
    -- for which we have bounds for the`n`-th derivative.
    _ ‚â§ ‚àë i in Finset.range (n + 1),
        (n.choose i : ‚Ñù) * ‚ÄñiteratedFDerivWithin ùïú i (fderivWithin ùïú g t ‚àò f) s x‚Äñ *
          ‚ÄñiteratedFDerivWithin ùïú (n - i) (fderivWithin ùïú f s) s x‚Äñ := by
      have A : ContDiffOn ùïú n (fderivWithin ùïú g t ‚àò f) s := by
        apply ContDiffOn.comp _ (hf.of_le M.le) hst
        apply hg.fderivWithin ht
        simp only [Nat.cast_succ, le_refl]
      have B : ContDiffOn ùïú n (fderivWithin ùïú f s) s := by
        apply hf.fderivWithin hs
        simp only [Nat.cast_succ, le_refl]
      exact (ContinuousLinearMap.compL ùïú E Fu Gu).norm_iteratedFDerivWithin_le_of_bilinear_of_le_one
        A B hs hx le_rfl (ContinuousLinearMap.norm_compL_le ùïú E Fu Gu)
    -- bound each of the terms using the estimates on previous derivatives (that use the inductive
    -- assumption for `g' ‚àò f`).
    _ ‚â§ ‚àë i in Finset.range (n + 1), (n.choose i : ‚Ñù) * (i ! * C * D ^ i) * D ^ (n - i + 1) := by
      gcongr with i hi
      ¬∑ simp only [mul_assoc (n.choose i : ‚Ñù)]
        exact I i hi
      ¬∑ exact J i
    -- We are left with trivial algebraic manipulations to see that this is smaller than
    -- the claimed bound.
    _ = ‚àë i in Finset.range (n + 1),
      -- porting note: had to insert a few more explicit type ascriptions in this and similar
      -- expressions.
        (n ! : ‚Ñù) * ((i ! : ‚Ñù)‚Åª¬π * i !) * C * (D ^ i * D ^ (n - i + 1)) * ((n - i)! : ‚Ñù)‚Åª¬π := by
      congr! 1 with i hi
      simp only [Nat.cast_choose ‚Ñù (Finset.mem_range_succ_iff.1 hi), div_eq_inv_mul, mul_inv]
      ring
    _ = ‚àë i in Finset.range (n + 1), (n ! : ‚Ñù) * 1 * C * D ^ (n + 1) * ((n - i)! : ‚Ñù)‚Åª¬π := by
      congr! with i hi
      ¬∑ apply inv_mul_cancel
        simpa only [Ne.def, Nat.cast_eq_zero] using i.factorial_ne_zero
      ¬∑ rw [‚Üê pow_add]
        congr 1
        rw [Nat.add_succ, Nat.succ_inj']
        exact Nat.add_sub_of_le (Finset.mem_range_succ_iff.1 hi)
    _ ‚â§ ‚àë i in Finset.range (n + 1), (n ! : ‚Ñù) * 1 * C * D ^ (n + 1) * 1 := by
      gcongr with i
      apply inv_le_one
      simpa only [Nat.one_le_cast] using (n - i).factorial_pos
    _ = (n + 1)! * C * D ^ (n + 1) := by
      simp only [mul_assoc, mul_one, Finset.sum_const, Finset.card_range, nsmul_eq_mul,
        Nat.factorial_succ, Nat.cast_mul]
#align norm_iterated_fderiv_within_comp_le_aux norm_iteratedFDerivWithin_comp_le_aux

/-- If the derivatives within a set of `g` at `f x` are bounded by `C`, and the `i`-th derivative
within a set of `f` at `x` is bounded by `D^i` for all `1 ‚â§ i ‚â§ n`, then the `n`-th derivative
of `g ‚àò f` is bounded by `n! * C * D^n`. -/
theorem norm_iteratedFDerivWithin_comp_le {g : F ‚Üí G} {f : E ‚Üí F} {n : ‚Ñï} {s : Set E} {t : Set F}
    {x : E} {N : ‚Ñï‚àû} (hg : ContDiffOn ùïú N g t) (hf : ContDiffOn ùïú N f s) (hn : (n : ‚Ñï‚àû) ‚â§ N)
    (ht : UniqueDiffOn ùïú t) (hs : UniqueDiffOn ùïú s) (hst : MapsTo f s t) (hx : x ‚àà s) {C : ‚Ñù}
    {D : ‚Ñù} (hC : ‚àÄ i, i ‚â§ n ‚Üí ‚ÄñiteratedFDerivWithin ùïú i g t (f x)‚Äñ ‚â§ C)
    (hD : ‚àÄ i, 1 ‚â§ i ‚Üí i ‚â§ n ‚Üí ‚ÄñiteratedFDerivWithin ùïú i f s x‚Äñ ‚â§ D ^ i) :
    ‚ÄñiteratedFDerivWithin ùïú n (g ‚àò f) s x‚Äñ ‚â§ n ! * C * D ^ n := by
  /- We reduce the bound to the case where all spaces live in the same universe (in which we
    already have proved the result), by using linear isometries between the spaces and their `ULift`
    to a common universe. These linear isometries preserve the norm of the iterated derivative. -/
  let Fu : Type max uF uG := ULift.{uG, uF} F
  let Gu : Type max uF uG := ULift.{uF, uG} G
  have isoF : Fu ‚âÉ‚Çó·µ¢[ùïú] F := LinearIsometryEquiv.ulift ùïú F
  have isoG : Gu ‚âÉ‚Çó·µ¢[ùïú] G := LinearIsometryEquiv.ulift ùïú G
  -- lift `f` and `g` to versions `fu` and `gu` on the lifted spaces.
  let fu : E ‚Üí Fu := isoF.symm ‚àò f
  let gu : Fu ‚Üí Gu := isoG.symm ‚àò g ‚àò isoF
  let tu := isoF ‚Åª¬π' t
  have htu : UniqueDiffOn ùïú tu := isoF.toContinuousLinearEquiv.uniqueDiffOn_preimage_iff.2 ht
  have hstu : MapsTo fu s tu := fun y hy ‚Ü¶ by
    simpa only [mem_preimage, comp_apply, LinearIsometryEquiv.apply_symm_apply] using hst hy
  have Ffu : isoF (fu x) = f x := by simp only [comp_apply, LinearIsometryEquiv.apply_symm_apply]
  -- All norms are preserved by the lifting process.
  have hfu : ContDiffOn ùïú n fu s := isoF.symm.contDiff.comp_contDiffOn (hf.of_le hn)
  have hgu : ContDiffOn ùïú n gu tu :=
    isoG.symm.contDiff.comp_contDiffOn
      ((hg.of_le hn).comp_continuousLinearMap (isoF : Fu ‚ÜíL[ùïú] F))
  have Nfu : ‚àÄ i, ‚ÄñiteratedFDerivWithin ùïú i fu s x‚Äñ = ‚ÄñiteratedFDerivWithin ùïú i f s x‚Äñ := fun i ‚Ü¶ by
    rw [LinearIsometryEquiv.norm_iteratedFDerivWithin_comp_left _ _ hs hx]
  simp_rw [‚Üê Nfu] at hD
  have Ngu : ‚àÄ i,
      ‚ÄñiteratedFDerivWithin ùïú i gu tu (fu x)‚Äñ = ‚ÄñiteratedFDerivWithin ùïú i g t (f x)‚Äñ := fun i ‚Ü¶ by
    rw [LinearIsometryEquiv.norm_iteratedFDerivWithin_comp_left _ _ htu (hstu hx)]
    rw [LinearIsometryEquiv.norm_iteratedFDerivWithin_comp_right _ _ ht, Ffu]
    rw [Ffu]
    exact hst hx
  simp_rw [‚Üê Ngu] at hC
  have Nfgu :
      ‚ÄñiteratedFDerivWithin ùïú n (g ‚àò f) s x‚Äñ = ‚ÄñiteratedFDerivWithin ùïú n (gu ‚àò fu) s x‚Äñ := by
    have : gu ‚àò fu = isoG.symm ‚àò g ‚àò f := by
      ext x
      simp only [comp_apply, LinearIsometryEquiv.map_eq_iff, LinearIsometryEquiv.apply_symm_apply]
    rw [this, LinearIsometryEquiv.norm_iteratedFDerivWithin_comp_left _ _ hs hx]
  -- deduce the required bound from the one for `gu ‚àò fu`.
  rw [Nfgu]
  exact norm_iteratedFDerivWithin_comp_le_aux hgu hfu htu hs hstu hx hC hD
#align norm_iterated_fderiv_within_comp_le norm_iteratedFDerivWithin_comp_le

/-- If the derivatives of `g` at `f x` are bounded by `C`, and the `i`-th derivative
of `f` at `x` is bounded by `D^i` for all `1 ‚â§ i ‚â§ n`, then the `n`-th derivative
of `g ‚àò f` is bounded by `n! * C * D^n`. -/
theorem norm_iteratedFDeriv_comp_le {g : F ‚Üí G} {f : E ‚Üí F} {n : ‚Ñï} {N : ‚Ñï‚àû} (hg : ContDiff ùïú N g)
    (hf : ContDiff ùïú N f) (hn : (n : ‚Ñï‚àû) ‚â§ N) (x : E) {C : ‚Ñù} {D : ‚Ñù}
    (hC : ‚àÄ i, i ‚â§ n ‚Üí ‚ÄñiteratedFDeriv ùïú i g (f x)‚Äñ ‚â§ C)
    (hD : ‚àÄ i, 1 ‚â§ i ‚Üí i ‚â§ n ‚Üí ‚ÄñiteratedFDeriv ùïú i f x‚Äñ ‚â§ D ^ i) :
    ‚ÄñiteratedFDeriv ùïú n (g ‚àò f) x‚Äñ ‚â§ n ! * C * D ^ n := by
  simp_rw [‚Üê iteratedFDerivWithin_univ] at hC hD ‚ä¢
  exact norm_iteratedFDerivWithin_comp_le hg.contDiffOn hf.contDiffOn hn uniqueDiffOn_univ
    uniqueDiffOn_univ (mapsTo_univ _ _) (mem_univ x) hC hD
#align norm_iterated_fderiv_comp_le norm_iteratedFDeriv_comp_le

section Apply

theorem norm_iteratedFDerivWithin_clm_apply {f : E ‚Üí F ‚ÜíL[ùïú] G} {g : E ‚Üí F} {s : Set E} {x : E}
    {N : ‚Ñï‚àû} {n : ‚Ñï} (hf : ContDiffOn ùïú N f s) (hg : ContDiffOn ùïú N g s) (hs : UniqueDiffOn ùïú s)
    (hx : x ‚àà s) (hn : ‚Üën ‚â§ N) : ‚ÄñiteratedFDerivWithin ùïú n (fun y => (f y) (g y)) s x‚Äñ ‚â§
      ‚àë i in Finset.range (n + 1), ‚Üë(n.choose i) * ‚ÄñiteratedFDerivWithin ùïú i f s x‚Äñ *
        ‚ÄñiteratedFDerivWithin ùïú (n - i) g s x‚Äñ := by
  let B : (F ‚ÜíL[ùïú] G) ‚ÜíL[ùïú] F ‚ÜíL[ùïú] G := ContinuousLinearMap.flip (ContinuousLinearMap.apply ùïú G)
  have hB : ‚ÄñB‚Äñ ‚â§ 1 := by
    simp only [ContinuousLinearMap.op_norm_flip, ContinuousLinearMap.apply]
    refine' ContinuousLinearMap.op_norm_le_bound _ zero_le_one fun f => _
    simp only [ContinuousLinearMap.coe_id', id.def, one_mul]
    rfl
  exact B.norm_iteratedFDerivWithin_le_of_bilinear_of_le_one hf hg hs hx hn hB
#align norm_iterated_fderiv_within_clm_apply norm_iteratedFDerivWithin_clm_apply

theorem norm_iteratedFDeriv_clm_apply {f : E ‚Üí F ‚ÜíL[ùïú] G} {g : E ‚Üí F} {N : ‚Ñï‚àû} {n : ‚Ñï}
    (hf : ContDiff ùïú N f) (hg : ContDiff ùïú N g) (x : E) (hn : ‚Üën ‚â§ N) :
    ‚ÄñiteratedFDeriv ùïú n (fun y : E => (f y) (g y)) x‚Äñ ‚â§ ‚àë i in Finset.range (n + 1),
      ‚Üë(n.choose i) * ‚ÄñiteratedFDeriv ùïú i f x‚Äñ * ‚ÄñiteratedFDeriv ùïú (n - i) g x‚Äñ := by
  simp only [‚Üê iteratedFDerivWithin_univ]
  exact norm_iteratedFDerivWithin_clm_apply hf.contDiffOn hg.contDiffOn uniqueDiffOn_univ
    (Set.mem_univ x) hn
#align norm_iterated_fderiv_clm_apply norm_iteratedFDeriv_clm_apply

theorem norm_iteratedFDerivWithin_clm_apply_const {f : E ‚Üí F ‚ÜíL[ùïú] G} {c : F} {s : Set E} {x : E}
    {N : ‚Ñï‚àû} {n : ‚Ñï} (hf : ContDiffOn ùïú N f s) (hs : UniqueDiffOn ùïú s) (hx : x ‚àà s) (hn : ‚Üën ‚â§ N) :
    ‚ÄñiteratedFDerivWithin ùïú n (fun y : E => (f y) c) s x‚Äñ ‚â§
      ‚Äñc‚Äñ * ‚ÄñiteratedFDerivWithin ùïú n f s x‚Äñ := by
  let g : (F ‚ÜíL[ùïú] G) ‚ÜíL[ùïú] G := ContinuousLinearMap.apply ùïú G c
  have h := g.norm_compContinuousMultilinearMap_le (iteratedFDerivWithin ùïú n f s x)
  rw [‚Üê g.iteratedFDerivWithin_comp_left hf hs hx hn] at h
  refine' h.trans (mul_le_mul_of_nonneg_right _ (norm_nonneg _))
  refine' g.op_norm_le_bound (norm_nonneg _) fun f => _
  rw [ContinuousLinearMap.apply_apply, mul_comm]
  exact f.le_op_norm c
#align norm_iterated_fderiv_within_clm_apply_const norm_iteratedFDerivWithin_clm_apply_const

theorem norm_iteratedFDeriv_clm_apply_const {f : E ‚Üí F ‚ÜíL[ùïú] G} {c : F} {x : E} {N : ‚Ñï‚àû} {n : ‚Ñï}
    (hf : ContDiff ùïú N f) (hn : ‚Üën ‚â§ N) :
    ‚ÄñiteratedFDeriv ùïú n (fun y : E => (f y) c) x‚Äñ ‚â§ ‚Äñc‚Äñ * ‚ÄñiteratedFDeriv ùïú n f x‚Äñ := by
  simp only [‚Üê iteratedFDerivWithin_univ]
  exact norm_iteratedFDerivWithin_clm_apply_const hf.contDiffOn uniqueDiffOn_univ
    (Set.mem_univ x) hn
#align norm_iterated_fderiv_clm_apply_const norm_iteratedFDeriv_clm_apply_const

end Apply
